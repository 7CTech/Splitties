/*
 * Copyright (c) 2016. Louis Cognault Ayeva Derman
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion projectSdkVersion
    buildToolsVersion projectBuildToolsVersion
    defaultConfig {
        applicationId "xyz.louiscad.splittiessample"
        minSdkVersion 14
        targetSdkVersion projectSdkVersion
        versionCode 1
        versionName "1.0"
        resConfigs "en", "fr"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst "META-INF/appctx_release.kotlin_module"
    }
}

dependencies {
    // This project's libraries
    debugImplementation project(':appctx')
    debugImplementation project(':stetho-init')
    debugImplementation project(':concurrency')
    debugImplementation project(':preferences')
    debugImplementation project(':typesaferecyclerview')
    debugImplementation project(':selectableviews')
    debugImplementation project(':material-lists')

    releaseImplementation "xyz.louiscad.splitties:splitties-appctx:$splittiesVersion"
    releaseImplementation "xyz.louiscad.splitties:splitties-concurrency:$splittiesVersion"
    releaseImplementation "xyz.louiscad.splitties:splitties-preferences:$splittiesVersion"
    releaseImplementation "xyz.louiscad.splitties:splitties-typesaferecyclerview:$splittiesVersion"
    releaseImplementation "xyz.louiscad.splitties:splitties-selectableviews:$splittiesVersion"
    releaseImplementation "xyz.louiscad.splitties:splitties-material-lists:$splittiesVersion"

    // Android Support Libraries
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    // Constraint Layout
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Timber log utility
    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation 'com.dmitrymalkovich.android:material-design-dimens:1.2'
    implementation 'com.actinarium.aligned:aligned:0.1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Kotlinx coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.20"

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

apply from: '../no-version-ranges.gradle'
