/*
 * Copyright (c) 2016. Louis Cognault Ayeva Derman
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion projectSdkVersion
    buildToolsVersion projectBuildToolsVersion
    defaultConfig {
        applicationId "xyz.louiscad.splittiessample"
        minSdkVersion 14
        targetSdkVersion projectSdkVersion
        versionCode 1
        versionName "1.0"
        resConfigs "en", "fr"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst "META-INF/appctx_release.kotlin_module"
    }
}

dependencies {
    // This project's libraries
    debugApi project(':appctx')
    debugImplementation project(':stetho-init')
    debugApi project(':concurrency')
    debugApi project(':preferences')
    debugApi project(':typesaferecyclerview')
    debugApi project(':selectableviews')
    debugApi project(':material-lists')

    releaseApi "xyz.louiscad.splitties:splitties-appctx:$splittiesVersion"
    releaseApi "xyz.louiscad.splitties:splitties-concurrency:$splittiesVersion"
    releaseApi "xyz.louiscad.splitties:splitties-preferences:$splittiesVersion"
    releaseApi "xyz.louiscad.splitties:splitties-typesaferecyclerview:$splittiesVersion"
    releaseApi "xyz.louiscad.splitties:splitties-selectableviews:$splittiesVersion"
    releaseApi "xyz.louiscad.splitties:splitties-material-lists:$splittiesVersion"

    // Android Support Libraries
    api "com.android.support:appcompat-v7:$supportLibVersion"
    api "com.android.support:design:$supportLibVersion"

    // Constraint Layout
    api "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Timber log utility
    api "com.jakewharton.timber:timber:$timberVersion"

    api 'com.dmitrymalkovich.android:material-design-dimens:1.2'
    api 'com.actinarium.aligned:aligned:0.1'

    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Kotlinx coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.17"

    // Tests
    testApi 'junit:junit:4.12'
    androidTestApi('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

apply from: '../no-version-ranges.gradle'
