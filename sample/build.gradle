/*
 * Copyright (c) 2016. Louis Cognault Ayeva Derman
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion projectSdkVersion
    buildToolsVersion projectBuildToolsVersion
    defaultConfig {
        applicationId "xyz.louiscad.splittiessample"
        minSdkVersion 14
        targetSdkVersion projectSdkVersion
        versionCode 1
        versionName "1.0"
        resConfigs "en", "fr"
        vectorDrawables.useSupportLibrary = true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        dev {
            initWith(buildTypes.debug)
            applicationIdSuffix ".dev"
            versionNameSuffix "-DEV"
            minifyEnabled true
            useProguard false
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            minifyEnabled true
            useProguard false
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    // This project's libraries
    compile project(':appctx')
    devCompile project(':concurrency')
    devCompile project(':typesaferecyclerview')
    devCompile project(':selectableviewgroups')
    compile project(':preferences')
    compile project(':material-lists')

    debugCompile "xyz.louiscad.splitties:concurrency:${splittiesVersion}"
    debugCompile "xyz.louiscad.splitties:typesaferecyclerview:${splittiesVersion}"
    debugCompile "xyz.louiscad.splitties:selectableviewgroups:${splittiesVersion}"

    releaseCompile "xyz.louiscad.splitties:concurrency-unchecked:${splittiesVersion}"
    releaseCompile "xyz.louiscad.splitties:typesaferecyclerview:${splittiesVersion}"
    releaseCompile "xyz.louiscad.splitties:selectableviewgroups:${splittiesVersion}"

    // Android Support Libraries
    compile "com.android.support:appcompat-v7:${supportLibVersion}";
    compile "com.android.support:design:${supportLibVersion}";

    // Constraint Layout
    compile 'com.android.support.constraint:constraint-layout:1.0.0'

    // Timber log utility
    compile "com.jakewharton.timber:timber:${timberVersion}";

    compile 'com.dmitrymalkovich.android:material-design-dimens:1.2'
    compile 'com.actinarium.aligned:aligned:0.1'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Tests
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://dl.bintray.com/louiscad/splitties"
    }
}